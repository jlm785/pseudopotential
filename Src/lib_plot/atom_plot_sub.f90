!>  Plots wave-functions and potentials from the information in
!>  in a file generated by atom_sub and psd_sub (plot.dat)
!>
!>  \author       Peter Schuster, Manuel Maria Alemany, Jose Luis Martins
!>  \version      6.0.5
!>  \date         April 1993, January 2000, 6 August 2021, 21 October 2021.
!>  \copyright    GNU Public License v2

subroutine atom_plot_sub(iowrite, ioplot, fname, iost, iocomm, iotmp, lint)

! written by Peter Schuster, April 1993
! interative ploting added by Manuel Maria Alemany, January 2000
! removed H formats 22 January 2008. JLM
! converted to fortran 90, 6 August 2021
! printing. iowrite. NEW interface. 21 October 2021. JLM

  implicit none

  integer, parameter          :: REAL64 = selected_real_kind(12)

! input

  integer, intent(in)               ::  iowrite                          !<  default tape for writing

  integer, intent(in)               ::  ioplot                           !<  tape to read input

  character(len=*), intent(in)      ::  fname                            !<  name of file with plot information

  integer, intent(in)               ::  iost                             !<  data files use tapes iost+1 to iost+4
  integer, intent(in)               ::  iocomm                           !<  tape number for gnuplot command file
  integer, intent(in)               ::  iotmp                            !<  tape number for temporary files

  logical, intent(in)               ::  lint                             !<  interactive run

! dimensions

  integer                           ::  nmax                             !  dimension of number of points
  integer                           ::  numw                             !  dimension of number of true wave-functions
  integer                           ::  nump                             !  dimension of number of pseudo-wave-functions
  integer                           ::  nftp                             !  dimension of number of Fourier transform points

! data of plots

  integer                           ::  iw                               !  number of true wave-functions
  character(len=12), allocatable    ::  labelw(:)                        !  label of true wave-functions
  integer, allocatable              ::  numbw(:)                         !  index of true wave-functions
  real(REAL64), allocatable         ::  wx(:,:), wy(:,:)                 !  true wave-functions

  integer                           ::  ip                               !  number of pseudo-wave-functions
  character(len=12), allocatable    ::  labelp(:)                        !  label of pseudo-wave-functions
  integer, allocatable              ::  numbp(:)                         !  index of pseudo-wave-functions
  real(REAL64), allocatable         ::  px(:,:), py(:,:)                 !  pseudo wave-functions

  integer                           ::  iv                               !  number of pseudo-potentials
  character(len=12), allocatable    ::  labelv(:)                        !  label of pseudo-potentials
  integer, allocatable              ::  numbv(:)                         !  index of pseudo-potentials
  real(REAL64), allocatable         ::  vx(:,:), vy(:,:)                 !  pseudo-potentials

  integer                           ::  ifp                              !  number of pseudo-potentials transforms
  character(len=12), allocatable    ::  labelf(:)                        !  label of pseudo-potentials transforms
  integer, allocatable              ::  numbf(:)                         !  index of pseudopotentials transforms
  real(REAL64), allocatable         ::  fx(:,:), fy(:,:)                 !  pseudo-potentials transforms

  integer                           ::  ifw                              !  number of pseudo-wave-functions transforms
  character(len=12), allocatable    ::  labelfw(:)                       !  label of pseudo-wave-function transforms
  integer, allocatable              ::  numbfw(:)                        !  index of pseudo-wave-function transforms
  real(REAL64), allocatable         ::  fwx(:,:), fwy(:,:)               !  pseudo-wave-function transforms

! other variables

  real(REAL64)                      ::  zion                             !  ionic charge
  integer                           ::  iwsave, ipsave
  real(REAL64)                      ::  yminwp, ymaxwp, yminv


  write(6,*)
  write(6,*) '  Generating plots for pseudopotentials and wave-functions'
  write(6,*)

! opening file to read from

  open(unit=ioplot, file=trim(fname), status='old', form='formatted')


  call atom_plot_input_size(ioplot, nmax, numw, nump, nftp)

  if(nmax < 1 .or. nftp < 1 .or. numw < 1 .or. nump <1) then

    write(6,*)
    write(6,*) ' non positive dimension in atom_plot_show'
    write(6,*) ' nmax, numw, nump, nftp = ',nmax, numw, nump, nftp
    write(6,*) ' returning without showing plots'
    write(6,*)

    return

  endif

  allocate(labelw(numw),numbw(numw))
  allocate(wx(nmax,numw),wy(nmax,numw))

  allocate(labelp(numw),numbp(numw))
  allocate(px(nmax,numw),py(nmax,numw))

  allocate(labelv(nump),numbv(nump))
  allocate(vx(nmax,nump),vy(nmax,nump))

  allocate(labelf(nump),numbf(nump))
  allocate(fx(nftp,nump),fy(nftp,nump))

  allocate(labelfw(nump),numbfw(nump))
  allocate(fwx(nftp,nump),fwy(nftp,nump))

! it is a tape!

  rewind(ioplot)

  call atom_plot_input(ioplot, nmax, numw, nump, nftp,                   &
      wx, wy, px, py, vx, vy, fx, fy, fwx, fwy,                          &
      numbw, numbp, numbv, numbf, numbfw,                                &
      iw, ip, iv, ifp, ifw,                                              &
      labelw, labelp, labelv, labelf, labelfw, zion,                     &
      iowrite)

  close(ioplot)

  iwsave = iw
  ipsave = ip

  call atom_plot_outdat(iost, nmax, numw, nump, nftp,                    &
      wx, wy, py, vx, vy, fx, fy, fwx, fwy,                              &
      numbw, numbv, numbf, numbfw,                                       &
      iw, ip, iv, ifp, ifw,                                              &
      labelw, labelp, labelv, labelf, labelfw, yminwp, ymaxwp, yminv)

  iw = iwsave
  ip = ipsave

  if(lint) then

    call atom_plot_outdis(iowrite, iocomm, iotmp, numw, nump, zion,      &
        iw, ip, iv, ifp, ifw,                                            &
        labelw, labelp, labelv, labelf, labelfw, yminwp, ymaxwp, yminv)

  else

    call atom_plot_outpdf(iocomm, iotmp, numw, nump, zion,               &
        iw, ip, iv, ifp, ifw,                                            &
        labelw, labelp, labelv, labelf, labelfw, yminwp, ymaxwp, yminv)

  endif


  deallocate(labelw,numbw)
  deallocate(wx,wy)

  deallocate(labelp,numbp)
  deallocate(px,py)

  deallocate(labelv,numbv)
  deallocate(vx,vy)

  deallocate(labelf,numbf)
  deallocate(fx,fy)

  deallocate(labelfw,numbfw)
  deallocate(fwx,fwy)

  return

end subroutine atom_plot_sub

