!>  Plots wave-functions and potentials from the information in
!>  in a file generated by atom_sub and psd_sub (plot.dat)
!>
!>  \author       Peter Schuster, Manuel Maria Alemany, Jose Luis Martins
!>  \version      6.0.6
!>  \date         April 1993, January 2000, 4 September 2021, 9 October 2021.
!>  \copyright    GNU Public License v2

subroutine atom_plot_kb_sub(iowrite, ioplot, fname, iost, iocomm, iotmp, lint)

! adapted from the modified all-elecron+pseudopotential code
! kinetic, 9 October 2021. JLM
! nump, nftp can be zero. 18 and 28 October 2021. JLM
! printing. 21 October 2021. JLM

  implicit none

  integer, parameter          :: REAL64 = selected_real_kind(12)

! input

  integer, intent(in)               ::  iowrite                          !<  default tape for writing

  integer, intent(in)               ::  ioplot                           !<  tape to read input

  character(len=*), intent(in)      ::  fname                            !<  name of file with plot information

  integer, intent(in)               ::  iost                             !<  data files use tapes iost+1 to iost+4
  integer, intent(in)               ::  iocomm                           !<  tape number for gnuplot command file
  integer, intent(in)               ::  iotmp                            !<  tape number for temporary files

  logical, intent(in)               ::  lint                             !<  interactive run

! dimensions

  integer                           ::  nmax                             !  dimension of number of points
  integer                           ::  numw                             !  dimension of number of wave-functions
  integer                           ::  numb                             !  dimension of number of basis-functions
  integer                           ::  nump                             !  dimension of number of projectors
  integer                           ::  nftp                             !  dimension of number of Fourier transform points

! data of plots

  character(len=12)                 ::  labelv                           !  label of local potential
  integer                           ::  numbv                            !  number of points for local potential
  real(REAL64), allocatable         ::  vr(:), vk(:)                     !  local potential

  integer                           ::  iw                               !  number of wave-functions
  character(len=12), allocatable    ::  labelw(:)                        !  label of wave-functions
  integer, allocatable              ::  numbw(:)                         !  index of wave-functions
  integer, allocatable              ::  lo_w(:)                          !  angular momentum of wave-functions
  real(REAL64), allocatable         ::  wx(:,:), wy(:,:)                 !  wave-functions

  integer                           ::  ib                               !  number of basis-functions
  character(len=12), allocatable    ::  labelb(:)                        !  label of basis-functions
  integer, allocatable              ::  numbb(:)                         !  index of basis-functions
  integer, allocatable              ::  lo_b(:)                          !  angular momentum of basis-functions
  real(REAL64), allocatable         ::  bx(:,:), by(:,:)                 !  basis-functions

  integer                           ::  ip                               !  number of projectors
  character(len=12), allocatable    ::  labelp(:)                        !  label of projectors
  integer, allocatable              ::  numbp(:)                         !  index of projectors
  integer, allocatable              ::  lo_p(:)                          !  angular momentum of projectors
  real(REAL64), allocatable         ::  px(:,:), py(:,:)                 !  KB projectors (real space)

  integer                           ::  iq                               !  number of projectors
  character(len=12), allocatable    ::  labelq(:)                        !  label of projectors
  integer, allocatable              ::  numbq(:)                         !  index of projectors
  integer, allocatable              ::  lo_q(:)                          !  angular momentum of projectors
  real(REAL64), allocatable         ::  qx(:,:), qy(:,:)                 !  KB projectors (reciprocal space)

  character(len=12)                 ::  labelek                          !  label of kinetic energy
  integer                           ::  numbek                           !  number of q points for kinetic energy
  real(REAL64), allocatable         ::  qek(:), ek(:)                    !  kinetic energy integral

! other variables

  integer                           ::  iwsave, ibsave
  real(REAL64)                      ::  yminwp, ymaxwp
  real(REAL64)                      ::  yminp, ymaxp
  real(REAL64)                      ::  yminq, ymaxq
  real(REAL64)                      ::  xmaxek, yminek, ymaxek
  real(REAL64)                      ::  yminv, ymaxv


  write(6,*)
  write(6,*) '  Generating plots for KB projectors and basis functions'
  write(6,*)

! opening file to read from

  open(unit=ioplot, file=trim(fname), status='old', form='formatted')


  call atom_plot_kb_input_size(ioplot, nmax, numw, numb, nump, nftp)

! it would be smarter to skip if they are zero...

  if(nmax < 1 .or. nftp < 0 .or. numw < 1 .or. numb <1 .or. nump <0) then

    write(6,*)
    write(6,*) ' non positive dimension in atom_plot_kb_sub'
    write(6,*) ' nmax, numw, numb, nump, nftp = ',nmax, numw, numb, nump, nftp
    write(6,*) ' returning without showing plots'
    write(6,*)

    return

  endif

  allocate(labelw(numw),numbw(numw))
  allocate(wx(nmax,numw),wy(nmax,numw))
  allocate(lo_w(numw))

  allocate(labelb(numb),numbb(numb))
  allocate(bx(nmax,numb),by(nmax,numb))
  allocate(lo_b(numb))

  if(nump /= 0) then
    allocate(labelp(nump),numbp(nump))
    allocate(px(nmax,nump),py(nmax,nump))
    allocate(lo_p(nump))
  else
    allocate(labelp(1),numbp(1))
    allocate(px(nmax,1),py(nmax,1))
    allocate(lo_p(1))
  endif


  if(nftp /= 0) then
    allocate(labelq(nftp),numbq(nftp))
    allocate(qx(nmax,nftp),qy(nmax,nftp))
    allocate(lo_q(nftp))
  else
    allocate(labelq(1),numbq(1))
    allocate(qx(nmax,1),qy(nmax,1))
    allocate(lo_q(1))
  endif

  allocate(qek(nmax), ek(nmax))

  allocate(vr(nmax), vk(nmax))

! it is a tape!

  rewind(ioplot)

  call atom_plot_kb_input(iowrite, ioplot,                               &
     nmax, numw, numb, nump, nftp,                                       &
     iw, labelw, numbw, lo_w, wx, wy,                                    &
     ib, labelb, numbb, lo_b, bx, by,                                    &
     ip, labelp, numbp, lo_p, px, py,                                    &
     iq, labelq, numbq, lo_q, qx, qy,                                    &
     labelek, numbek, qek, ek,                                           &
     labelv, numbv, vr, vk)

  close(ioplot)

  iwsave = iw
  ibsave = ib

  call atom_plot_kb_outdat(iost, nmax, numw, numb, nump, nftp,           &
     iw, labelw, numbw, lo_w, wx, wy,                                    &
     ib, labelb, numbb, lo_b, bx, by,                                    &
     ip, labelp, numbp,       px, py,                                    &
     iq, labelq, numbq,       qx, qy,                                    &
     labelek, numbek, qek, ek,                                           &
     labelv, numbv, vr, vk,                                              &
     yminwp, ymaxwp, yminp, ymaxp, yminq, ymaxq,                         &
     xmaxek, yminek, ymaxek, yminv, ymaxv)

  iw = iwsave
  ib = ibsave

  if(lint) then

  call atom_plot_kb_outdis(iowrite, iocomm, iotmp,                       &
     numw, numb, nump, nftp,                                             &
     iw, labelw, lo_w,                                                   &
     ib, labelb, lo_b,                                                   &
     ip, labelp,                                                         &
     iq, labelq,                                                         &
     labelek,                                                            &
     labelv,                                                             &
     yminwp, ymaxwp, yminp, ymaxp, yminq, ymaxq,                         &
     xmaxek, yminek, ymaxek, yminv, ymaxv)

  else

    call atom_plot_kb_outpdf(iocomm, iotmp, numw, numb, nump, nftp,      &
     iw, labelw, lo_w,                                                   &
     ib, labelb, lo_b,                                                   &
     ip, labelp,                                                         &
     iq, labelq,                                                         &
     labelek,                                                            &
     labelv,                                                             &
     yminwp, ymaxwp, yminp, ymaxp, yminq, ymaxq,                         &
     xmaxek, yminek, ymaxek, yminv, ymaxv)

  endif


  deallocate(labelw,numbw)
  deallocate(wx,wy)
  deallocate(lo_w)

  deallocate(labelb,numbb)
  deallocate(bx,by)
  deallocate(lo_b)

  deallocate(labelp,numbp)
  deallocate(px,py)
  deallocate(lo_p)

  deallocate(labelq,numbq)
  deallocate(qx,qy)
  deallocate(lo_q)

  deallocate(qek,ek)

  deallocate(vr,vk)

  return

end subroutine atom_plot_kb_sub

